DESCRIPTION: Indirect response model to characterize longitudinal BCVA response

[LONGITUDINAL]
input = {EMAX,
	 C50,
	 k_out, 
         VA0,
	 AGE
} 

VA0 = {use = regressor}
AGE = {use = regressor}

; --------------------------------------------------------------------------
; Model equations
; -------------------------------------------------------------------------- 

EQUATION:
odeType = stiff

; --------------------------------------------------------------------------
; Fixed PK
; -------------------------------------------------------------------------- 

; Fixed pk - half life 6 days
; Volume fixed to 4 mL, as per gRED estimate

CC = pkmodel(k = log(2) / 6, V = 4)

; --------------------------------------------------------------------------
; Sham (disease progression) model
; -------------------------------------------------------------------------- 
; from sham_trans model

k_out_sham = 0.0038
VAss_sham  = 0.1 + 0.55 * (VA0 / 2) - 0.0041 * AGE

; --------------------------------------------------------------------------
; Drug effect model
; -------------------------------------------------------------------------- 

; Drug inhibits k_out
k_out_trt  = k_out_sham * (1 + k_out * CC / (C50 + CC))

; Drug stimulates VAss (i.e. k_in)
; EMAX average number (for VA0 = 50 or 1 on trans scale) gained under trt
EMAX_TRT   = VA0 + EMAX 
VAss_trt   = VAss_sham + (EMAX_TRT - VAss_sham) * CC / (C50 + CC)  
k_in_trt   = VAss_trt * k_out_trt

; --------------------------------------------------------------------------
; Indirect response
; -------------------------------------------------------------------------- 

; initial condition: start from baseline
VA_0 = VA0
ddt_VA = k_in_trt - k_out_trt * VA

; --------------------------------------------------------------------------
; Ouputs 
; --------------------------------------------------------------------------

OUTPUT:
output= {VA}
table = {VA, CC}